group = 'com.haunted.gradle-plugins'
version = '0.1.1'

apply plugin: 'groovy'
apply plugin: 'maven'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy:2.+'
    compile gradleApi()
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle SaveSSD plugin',
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "${bintray_api_base_url}/maven/${bintray_org}/${bintray_repo}/${bintray_package}") {
                authentication(userName: bintray_username, password: bintray_api_key)
            }
            pom.project {
                artifactId 'save-ssd'
                name 'SaveSSD'
                packaging 'jar'
                description 'Gradle plugin that helps to save your SSD write cycles by linking project build directory into /tmp on *nix.'
                url 'https://github.com/populov/gradle-save-ssd-plugin'
                inceptionYear 2013

                scm {
                    url 'https://github.com/populov/gradle-save-ssd-plugin'
                    connection 'git@github.com:populov/gradle-save-ssd-plugin.git'
                    developerConnection 'git@github.com:populov/gradle-save-ssd-plugin.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'Populov'
                        name 'Serge Populov'
                        email 'serge@populov.com'
                    }
                }
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.9'
}